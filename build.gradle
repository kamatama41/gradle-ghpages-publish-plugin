buildscript {
    repositories {
        jcenter()
        maven { url "http://kamatama41.github.com/maven-repository/repository" }
    }
    dependencies {
        classpath("com.github.kamatama41:gradle-git-release-plugin:+")
    }
}

plugins {
    id "com.gradle.plugin-publish" version "0.10.1"
    id "java-gradle-plugin"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "idea"
}
apply plugin: "com.github.kamatama41.git-release"

repositories {
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.github.kamatama41"

configurations {
    [compileClasspath, testCompileClasspath].each {
        it.resolutionStrategy.activateDependencyLocking()
    }
}
dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.ajoberstar:gradle-git-publish:0.3.2"
    implementation "net.researchgate:gradle-release:2.6.0"
    testImplementation gradleTestKit()
    testImplementation "junit:junit:4.12"
    testImplementation "org.eclipse.jgit:org.eclipse.jgit.junit:4.10.0.201712302008-r"
}

gitRelease {
    groupId = group
    repoUri = "git@github.com:kamatama41/maven-repository.git"
    repoDir = file("${System.getProperty("user.home")}/gh-maven-repository")
}

gradlePlugin {
    automatedPublishing = false

    plugins {
        gitReleasePlugin {
            id = "${group}.git-release"
            implementationClass = "com.github.kamatama41.gradle.gitrelease.GitReleasePlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/kamatama41/gradle-git-release-plugin"
    vcsUrl = "https://github.com/kamatama41/gradle-git-release-plugin"
    description = "A Gradle plugin that can release a Maven artifact into a Git repository, which is generally expected to be a GitHub Pages."
    tags = ["git", "maven"]

    plugins {
        gitReleasePlugin {
            displayName = "Gradle Git release plugin"
        }
    }
}

afterReleaseBuild.dependsOn publishPlugins
